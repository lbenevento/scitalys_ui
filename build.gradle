apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 30
        versionCode 1
        versionName "1.2.1-rc01"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    composeOptions {
        kotlinCompilerVersion "1.5.10"
        kotlinCompilerExtensionVersion "1.0.0-rc02"
    }

    buildFeatures {
        dataBinding true
        compose true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // App Compat
    implementation "androidx.appcompat:appcompat:$version_app_compat"

    // Compose
    implementation "androidx.compose.runtime:runtime:$version_compose"
    implementation "androidx.compose.ui:ui:$version_compose"
    implementation "androidx.compose.foundation:foundation:$version_compose"
    implementation "androidx.compose.foundation:foundation-layout:$version_compose"
    implementation "androidx.compose.material:material:$version_compose"
    implementation "androidx.compose.material:material:$version_compose"
    implementation "androidx.compose.runtime:runtime-livedata:$version_compose"
    implementation "androidx.compose.ui:ui-tooling:$version_compose"
    implementation "androidx.compose.material:material-icons-extended:$version_compose"
    implementation "androidx.activity:activity-compose:$version_compose_activity"
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha05"

    // Core KTX
    implementation "androidx.core:core-ktx:$version_core"

    // Material UI
    implementation "com.google.android.material:material:$version_material"

    // Scitalys
    implementation project(':bp_traits')
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Timber
    implementation "com.jakewharton.timber:timber:$version_timber"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}

//Publishing

apply plugin: 'maven-publish'

def env = System.getenv()
def github_usr = env["GITHUB_USR"]
def github_key = env["GITHUB_PACKAGES_KEY"]

def getVersionName = { ->
    return android.defaultConfig.versionName
}

def getArtifactId = { ->
    return "ui"
}

afterEvaluate {

    publishing {

        publications {
            bar(MavenPublication) {
                groupId 'com.scitalys'
                artifactId getArtifactId()
                version getVersionName()
                artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.api.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/beneventolorenzo/ui")
                credentials {
                    username = github_usr
                    password = github_key
                }
            }
        }

    }

}

task deleteArtifact(type: Delete) {
    delete "$buildDir/outputs/aar/${getArtifactId()}-release.aar"
}